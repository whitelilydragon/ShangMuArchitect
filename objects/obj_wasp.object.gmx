<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>spr_wasp</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>-5</depth>
  <persistent>0</persistent>
  <parentName>obj_enemy_floater</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///Physics and Animation Variables
event_inherited()
scr_enemy_hp(9)
legframe = 0
legoffset = 15
legsound = 0
var_scale = 1
can_hold_items = 0
dropped_goodies = obj_gold_gem
//Wasp Variables
timer = 0
tile_scanner_timer = 0
//Editor Mode Variables
start_x = x
start_y = y
width_x = sprite_width/2
height_y = sprite_height/2
flash_palette = spr_wasp_palette
death_type = 1
death_frame = 11
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///Logic and Animations
event_inherited()
if o.pause = 0 and death = 0 and freeze = 0
    {
        var_scale = 1;
        if item = spr_mod_chip_extra_large var_scale = 2;
        //Animation Loops
        switch(state)
            {
                //Idle
                case 0:
                    {
                        timer += 1
                        speed = 0

                        //Fly quickly based on difficulty scaling
                        var flight_speed = 6;
                        switch(o.user_enemy_level)
                            {
                                case 0: {flight_speed = 2; break;} //Easy
                                case 1: {flight_speed = 4; break;} //Normal
                                case 2: {flight_speed = 6; break;} //Hard
                                case 3: {flight_speed = 8; break;} //Hardcore
                                case 4: {flight_speed = 10; break;} //Insanity
                            }

                        //Movement
                        var sub_state = 0;
                        if (y &gt; start_y+8) sub_state = 1;
                        //Seeking Target
                        if sub_state = 0
                            {
                                //Clip to Spawn Height
                                y = start_y
                                //Movement
                                x += flight_speed*xdir
                                //Animations
                                if (frame &lt; 5) frame = 5;
                                frame += 0.35
                                if (frame &gt; 6.8) frame = 5;
                                //Wall Avoidance
                                tile_scanner_timer += 1
                                if tile_scanner_timer &gt;= 5
                                    {
                                        if xdir = 1
                                            {
                                                if tile_layer_find(surface_flats,x+36,y)
                                                or x &gt; room_width -32 or x &gt; start_x + 128
                                                    xdir = -1;
                                            }
                                        else
                                            {
                                                if tile_layer_find(surface_flats,x-36,y)
                                                or x &lt; 32 or x &lt; start_x - 128
                                                    xdir = 1;
                                            }
                                        //Directional Switching Tiles
                                        var tile_flag = tile_layer_find(surface_flags,x+(4*xdir),y);
                                        var turn_on_tile = 0;
                                        if tile_exists(tile_flag)
                                            {
                                                var tile_flag_bg = tile_get_background(tile_flag);
                                                if tile_flag_bg = tile_layer_changer
                                                    {
                                                        if xdir = 1  and turn_on_tile = 0 {xdir = -1; x -= 4; turn_on_tile = 1;}
                                                        if xdir = -1 and turn_on_tile = 0 {xdir =  1; x += 4; turn_on_tile = 1;}
                                                    }
                                            }
                                        tile_scanner_timer = 0
                                    }
                                //Search for Player's X/Y Position Below Facing Direction
                                if point_in_rectangle(o.px,o.py,x+(24*xdir),y+24,x+(128*xdir),y+128)
                                and o.user_cursor_mode = 0
                                    {
                                        state = 1
                                        timer = 0
                                    }
                            }
                        //Returning to Spawn Height
                        if sub_state = 1
                            {
                                y -= 4
                                //Animations
                                frame += 0.35
                                if (frame &gt; 2.8) frame = 0;
                            }
                        break;
                    }
                //Attacking
                case 1:
                    {
                        timer += 1

                        //Fly quickly based on difficulty scaling
                        var flight_speed = 6;
                        switch(o.user_enemy_level)
                            {
                                case 0: {flight_speed = 4; break;} //Easy
                                case 1: {flight_speed = 6; break;} //Normal
                                case 2: {flight_speed = 8; break;} //Hard
                                case 3: {flight_speed = 10; break;} //Hardcore
                                case 4: {flight_speed = 12; break;} //Insanity
                            }

                        if timer = 1
                            audio_play_sound(snd_dragonfly,50,false);

                        //Animations
                        if timer &gt; 12
                            {
                                frame += 0.35
                                if (frame &lt; 3) frame = 3;
                                if (frame &gt; 4.8) frame = 4.8;
                            }
                        else
                            {
                                frame += 0.35
                                if (frame &gt; 2.8) frame = 0;
                            }

                        if timer &gt; 12 and timer &lt; 24
                            {
                                speed = flight_speed
                                direction = point_direction(x,y,o.px,o.py)
                                //move_towards_point(o.px,o.py,speed)
                            }

                        if timer &gt; 24
                            {
                                speed = flight_speed
                                //Search For Positions To Deal Damage
                                if point_distance(o.universal_player_x,o.universal_player_y,x,y) &lt; 32
                                    {
                                        state = 2
                                        timer = 0
                                        if o.hurt[0] = 0 and o.hurt[1] = 0
                                        and o.shield_invuln = 32
                                        and not (o.shield_active = 1 and o.shield=spr_shield_metal)
                                            scr_enemy_deal_damage(0.4,1,2);
                                    }
                                //Bomb Cube
                                var instance_ID = collision_circle(x,y,32,obj_player_cube_alt,false,true);
                                if instance_exists(instance_ID)
                                    scr_enemy_bomb_cube_damage(instance_ID);
                            }
                        if tile_layer_find(surface_flats,x,y+16)
                        or x &gt; start_x + 128
                        or y &gt; start_y + 256
                            {
                                state = 2
                                timer = 0
                            }
                        break;
                    }
                //Resetting Behavior Loop
                case 2:
                    {
                        var recovery_time = 30;
                        //Recover quickly based on difficulty scaling
                        switch(o.user_enemy_level)
                            {
                                case 0: {recovery_time = 45; break;} //Easy
                                case 1: {recovery_time = 25; break;} //Normal
                                case 2: {recovery_time = 20; break;} //Hard
                                case 3: {recovery_time = 15; break;} //Hardcore
                                case 4: {recovery_time = 10; break;} //Insanity
                            }
                        speed = 0
                        timer += 1
                        frame += 0.35
                        if (frame &gt; 2.8) frame = 0;
                        if timer &gt; recovery_time
                            {
                                state = 0
                                timer = 0
                            }
                        break;
                    }
            }
    } //End of pause check
else
    speed = 0;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>event_inherited()

if room = rm_editor and o.user_cursor_mode = 1
    {
        draw_sprite_part(spr_water_type_sludge,0,0,0,4,32,start_x-144,start_y-16)
        draw_sprite_part(spr_water_type_sludge,0,28,0,4,32,start_x+144,start_y-16)
    }
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
